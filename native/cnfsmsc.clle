PGM        PARM(&MODE &WAITTM &REQKEY &IN_NBR &IN_RSN)

  /*  Define parameters passed to this program.                  */
  DCL &MODE     *CHAR  10
  DCL &WAITTM   *DEC  ( 5 0 )
  DCL &REQKEY   *CHAR   6
  DCL &IN_NBR   *CHAR  15
  DCL &IN_RSN   *CHAR  65

  /*  Define variables for request                              */
  DCL &CMD      *CHAR 32

  /*  Define variables for response                             */
  DCL &EOD       *LGL
  DCL &EOA       *LGL
  DCL &NODATA    *LGL

  /* Declare varialbes for both request and response            */
  DCL &DATALEN  *DEC  ( 5 0 )
  DCL &DATABUF  *CHAR 512

  /*  Include request and response data definitions             */
  INCLUDE SRCSTMF('smsapi.clleinc')

  /*  Declare local variables                                   */
  DCL &FIRST   *LGL

  /* Skip sending request if *NOSEND */
  IF (&MODE *EQ '*RCVONLY') THEN(GOTO RECV)
  ELSE IF (&MODE *NE '*SNDRCV') DO
    SNDPGMMSG MSGID(CPF9898) MSGF(QCPFMSG) MSGTYPE(*ESCAPE) +
              MSGDTA('Invalid mode')
  ENDDO

  /* Send the web service request                               */
  CHGVAR &CMD      'confirmsms'
  CHGVAR &ToNbr    &IN_NBR
  CHGVAR &Reason   &IN_RSN
  CHGVAR &DATALEN  %LEN(&SMSData)
  CHGVAR &DATABUF  &SMSData
  CALL PGM(ECNCT/ECCSNDREQ) PARM(&CMD &DATALEN &DATABUF &REQKEY)
  MONMSG MSGID(CPF0000) EXEC(DO)
    SNDPGMMSG MSG('Error calling ECCSNDREQ') MSGTYPE(*COMP)
    GOTO ENDPGM
  ENDDO

  /* Receive the response from the web service                  */
RECV:

  CHGVAR &DATALEN  %LEN(&Result)
  CHGVAR &DATABUF  ' '
  CALL PGM(ECNCT/ECCRCVRES) PARM(&WAITTM &REQKEY &EOD &EOA &NODATA +
                                 &DATALEN &DATABUF)
  MONMSG MSGID(CPF0000) EXEC(DO)
    SNDPGMMSG MSG('Error calling ECCRCVRES') MSGTYPE(*COMP)
    GOTO ENDPGM
  ENDDO

  IF (&EOD *AND &EOA *AND &NODATA) DO
    SNDPGMMSG MSGID(CPF9898) MSGF(QCPFMSG) MSGTYPE(*ESCAPE) +
              MSGDTA('Timeout waiting on response:' *BCAT &REQKEY)
    GOTO ENDPGM
  ENDDO

  CHGVAR &Result  %SST(&DATABUF 1 &DATALEN)
  SNDPGMMSG MSG(&SmsSts *BCAT '-' *BCAT &FrmNbr *BCAT '-' +
                  *BCAT &Message) MSGTYPE(*COMP)
  IF (&SmsSts *EQ 'SUCCESS') DO
    IF (&Message *EQ 'CANCEL') DO
      SNDPGMMSG MSG('Operation Cancelled') MSGTYPE(*COMP)
      GOTO ENDPGM
    ENDDO
    SNDPGMMSG MSG('Confirmation Received') MSGTYPE(*COMP)
  ENDDO
  ELSE DO
    SNDPGMMSG MSG('SMS Send Failed. Cancelling Operation')
    GOTO ENDPGM
  ENDDO


ENDPGM:


ENDPGM
