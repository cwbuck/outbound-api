PGM        PARM(&MODE &WAITTM &REQKEY)

  /*  Define parameters passed to this program.                  */
  DCL &MODE     *CHAR  10
  DCL &WAITTM   *DEC  ( 5 0 )
  DCL &REQKEY   *CHAR   6

  /*  Define variables for request                              */
  DCL &CMD      *CHAR 32

  /*  Define variables for response                             */
  DCL &EOD       *LGL
  DCL &EOA       *LGL
  DCL &NODATA    *LGL

  /* Declare varialbes for both request and response            */
  DCL &DATALEN  *DEC  ( 5 0 )
  DCL &DATABUF  *CHAR 512

  /*  Include request and response data definitions             */
  INCLUDE SRCMBR(ICNDBAPI)

  /*  Declare local variables                                   */
  DCL &FIRST   *LGL

  /* Skip sending request if *NOSEND */
  IF (&MODE *EQ '*RCVONLY') THEN(GOTO RECV)
  ELSE IF (&MODE *NE '*SNDRCV') DO
    SNDPGMMSG MSGID(CPF9898) MSGF(QCPFMSG) MSGTYPE(*ESCAPE) +
              MSGDTA('Invalid mode')
  ENDDO

  /* Send the web service request                               */
  CHGVAR &CMD      'getjoke'
  CHGVAR &D_CTGRY  'nerdy'
  CHGVAR &DATALEN  %LEN(&DATA)
  CHGVAR &DATABUF  &DATA
  CALL PGM(ECNCT/ECCSNDREQ) PARM(&CMD &DATALEN &DATABUF &REQKEY)
  MONMSG MSGID(CPF0000) EXEC(DO)
    SNDPGMMSG MSG('Error calling ECCSNDREQ') MSGTYPE(*COMP)
    GOTO ENDPGM
  ENDDO

  /* Receive the response from the web service                  */
RECV:

  CHGVAR &DATALEN  %LEN(&RETDATA)
  CHGVAR &DATABUF  ' '
  CALL PGM(ECNCT/ECCRCVRES) PARM(&WAITTM &REQKEY &EOD &EOA &NODATA +
                                 &DATALEN &DATABUF)
  MONMSG MSGID(CPF0000) EXEC(DO)
    SNDPGMMSG MSG('Error calling ECCRCVRES') MSGTYPE(*COMP)
    GOTO ENDPGM
  ENDDO

  IF (&EOD *AND &EOA *AND &NODATA) DO
    SNDPGMMSG MSGID(CPF9898) MSGF(QCPFMSG) MSGTYPE(*ESCAPE) +
              MSGDTA('Timeout waiting on response:' *BCAT &REQKEY)
    GOTO ENDPGM
  ENDDO

  CHGVAR &RETDATA  %SST(&DATABUF 1 &DATALEN)
  IF (&R_HTTPSTS *EQ '200') DO
    SNDPGMMSG MSG(&R_HTTPSTS *BCAT '-' *BCAT &R_TYPE *BCAT '-' +
                  *BCAT &R_VALUE) MSGTYPE(*COMP)
    IF (&R_TYPE *NE 'success') THEN(GOTO ENDPGM)
  ENDDO
  ELSE DO
    CHGVAR &RETDATA3  %SST(&DATABUF 1 &DATALEN)
    SNDPGMMSG MSG(&R3_HTTPSTS *BCAT '-' *BCAT &R3_ERROR) MSGTYPE(*COMP)
    GOTO ENDPGM
  ENDDO

  CHGVAR &EOD '0'
  DOUNTIL (&EOD)
    CHGVAR &DATALEN  %LEN(&RETDATA2)
    CHGVAR &DATABUF  ' '
    CALL PGM(ECNCT/ECCRCVRES) PARM(&WAITTM &REQKEY &EOD &EOA &NODATA +
                                   &DATALEN &DATABUF)
    MONMSG MSGID(CPF0000) EXEC(DO)
      SNDPGMMSG MSG('Error calling ECCRCVRES') MSGTYPE(*COMP)
      GOTO ENDPGM
    ENDDO
    CHGVAR &RETDATA2  %SST(&DATABUF 1 &DATALEN)
    SNDPGMMSG MSG(&R2_JOKE) MSGTYPE(*COMP)
  ENDDO


ENDPGM:


ENDPGM
